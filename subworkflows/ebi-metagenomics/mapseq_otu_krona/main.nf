
include { MAPSEQ             } from '../../../modules/ebi-metagenomics/mapseq/main'
include { MAPSEQ2BIOM        } from '../../../modules/ebi-metagenomics/mapseq2biom/main'
include { KRONA_KTIMPORTTEXT } from '../../../modules/ebi-metagenomics/krona/ktimporttext/main'

workflow MAPSEQ_OTU_KRONA {

    take:
    ch_fasta    // channel: [ val(meta), [ fasta ] ]
    ch_dbs      // channel: [ path(fasta), path(tax), path(otu), path(mscluster), val(label) ]

    main:

    ch_versions = Channel.empty()

    input = ch_fasta
        .combine(ch_dbs)
        .map { reads_meta, reads, db_meta, db_files ->
            def meta = reads_meta + ['db_id': db_meta.id, 'db_label': db_files[4]]
            def (fasta, tax, otu, mscluster, label) = db_files
            return [meta, reads, fasta, tax, otu, mscluster, label]
        }

    mapseq_in = input.map{ meta, reads, fasta, tax, otu, _mscluster, label -> 
        [meta, reads, fasta, tax, otu, label]
    }
    MAPSEQ(mapseq_in)
    ch_versions = ch_versions.mix(MAPSEQ.out.versions.first())

    mapseq2biom_in = MAPSEQ.out.mseq
        .join(input)
        .map { meta, mapseq_out, _reads, _fasta, _tax, _otu, mscluster, label ->
            [meta, mapseq_out, mscluster, label]
        }
    MAPSEQ2BIOM(mapseq2biom_in)
    ch_versions = ch_versions.mix(MAPSEQ2BIOM.out.versions.first())

    krona_in = MAPSEQ2BIOM.out.krona_input
        .map { meta, mapseq2biom_out -> [meta, mapseq2biom_out, meta.db_label] }
    KRONA_KTIMPORTTEXT(krona_in)
    ch_versions = ch_versions.mix(KRONA_KTIMPORTTEXT.out.versions.first())

    emit:
    mseq                  = MAPSEQ.out.mseq                   // channel: [ val(meta), [ mseq ] ]
    krona_input           = MAPSEQ2BIOM.out.krona_input       // channel: [ val(meta), [ txt ] ]
    biom_out              = MAPSEQ2BIOM.out.biom_out          // channel: [ val(meta), [ tsv ] ]
    biom_notaxid_out      = MAPSEQ2BIOM.out.biom_notaxid_out  // channel: [ val(meta), [ tsv ] ]
    html                  = KRONA_KTIMPORTTEXT.out.html       // channel: [ val(meta), [ html ] ]
    versions              = ch_versions                       // channel: [ versions.yml ]
}
